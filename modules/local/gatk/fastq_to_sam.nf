process GATK4_FASTQTOSAM {
    tag "$sample_id"
    label 'process_medium'

    conda (params.enable_conda ? "bioconda::gatk4=4.2.6.1" : null)
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/gatk4:4.2.6.1--hdfd78af_0':
        'quay.io/biocontainers/gatk4:4.2.6.1--hdfd78af_0' }"

    input:
    tuple val(sample_id), path(reads)

    output:
    tuple val(sample_id), path("*.bam")

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${sample_id}"
    def reads_command = "--FASTQ ${reads[0]} --FASTQ2 ${reads[1]}"

    def avail_mem = 3
    if (!task.memory) {
        log.info '[GATK FastqToSam] Available memory not known - defaulting to 3GB. Specify process memory requirements to change this.'
    } else {
        avail_mem = task.memory.giga
    }
    """
    gatk --java-options "-Xmx${avail_mem}g" FastqToSam \\
        $reads_command \\
        --OUTPUT ${prefix}.bam \\
        --SAMPLE_NAME $prefix \\
        --TMP_DIR . \\
        $args
    """
}
